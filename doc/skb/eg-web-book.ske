;;;;
;;;; eg-web-book        -- A variation of Manuel Web-book
;;;;
;;;;
;;;;           Author: Erick Gallesio [eg@essi.fr]
;;;;    Creation date: 11-Oct-2004 14:48 (eg)
;;;; Last file update:  8-Jun-2020 16:56 (eg)


(define *html-engine* (environment-engine (skribe-environment)))

;;
;; Change default chapter title builder
;;
(define (html-build-chapter-n-*section-title type n e)
  (let* ((doc     (ast-document n))
     (tparent (markup-ref doc 'title))
     (tnode   (markup-ref n 'title))
     (tnum    (markup-ref n 'number)))
    (list tparent
      (hrule :width 10. :align 'center)
      tnum
      (if tnum ". " "")
      tnode)))


;;;; ======================================================================
;;;;    toc-browsing ...
;;;; ======================================================================
(define (toc-browsing d c e)
  (let ((chaps (find-down chapter? (markup-body d))))
    (apply table :width 100. :border 0 :frame 'box :cellpadding 2 :cellspacing 4
       (tr (th :align 'center :colspan 2 :bg "#ACE919"
               (color :fg "darkolivegreen" (list (linebreak 1)
                                                 "Contents"
                                                 (linebreak 2)))))
       (tr :class "navnorm"
       (td "")
       (td :align 'left (ref :handle (handle d) :text "*Top*")))
       (map (lambda (x)
          (tr :class  (if (eq? x c) "navsel" "navnorm")
          (td :width 8. :align 'left :valign 'top
              (html-container-number (chapter-number x) e)
              (symbol "nbs"))
          (if (eq? x c)
              (td :align 'left
             (ref :handle (handle x))
             (linebreak)
             (toc :chapter #t :class "truc" :section #t :subsection #f
                  (handle c)))
              (td :align 'left (ref :handle (handle x))))))
        chaps))))


;; ----------------------------------------------------------------------
;;  Environment Customization ...
;; ----------------------------------------------------------------------
(define *border-bg* "#204875")

(customize-environment!
 (html
    :main-browing-extra #f
    :chapter-file #t
    :css "doc-style.css"
    :title-background *border-bg*
    :title-foreground "#FAFAFA"
    :section-title-background #f
    :section-title-foreground "darkolivegreen"
    :subsection-title-background #f
    :subsection-title-foreground "darkolivegreen"
    :subsubsection-title-background #f
    :left-margin-size 20.
    :left-margin-background *border-bg*
    :left-margin (lambda (n e)
                   (let ((d (ast-document n))
                         (c (ast-chapter n)))
                     (toc-browsing d c e)))))



(define-writer &document-author *html-engine*
  :before "<div id='doc-author'>"
  :action (lambda (n e) (output (&html-author :parent n (markup-body n)) e))
  :after "</div>")


(define-writer author *html-engine*
  :action (lambda (n e)
            (output (author-name n) e)
            (display "<br>\n")
            (output (author-affiliation n) e)
            (display "<br>\n")
            (for-each (lambda(line) (output line e) (display "<br>\n"))
                      (author-address n))))


(define-writer  &html-title  *html-engine*
  :before "<div id='doc-title'>\n"
  :after  "</div>\n")

(define-writer &html-footnotes *html-engine*    ;; This should be simpler !!!! 
  :action (lambda (n e)
            (let ((fs (env-ref e 'footnotes-seen)))
              (when (pair? fs)
                (let ((lst (reverse! (car fs))))
                  (unless (null? lst) 
                    ;; display the footnotes on the top of the footnotes-seen stack
                    (display "<div class='footnotes'>\n<hr>\n")
                    (printf "<b>Note~A:</b><br>\n" (if (> (length lst) 1) "s" ""))
                    (let Loop ((lst lst))
                      (output (&html-do-footnote :parent n (car lst)) e)
                      (unless (null? (cdr lst))
                        (Loop (cdr lst))))
                    (display "</div>")))
                ;; pop these footnotes from footnotes-seen 
                (env-set! e 'footnotes-seen (cdr fs))))))
